plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'com.google.dagger.hilt.android'
    id 'com.github.triplet.play' version '3.8.1'
    id 'com.google.protobuf'
}

def appVersionCode = Integer.valueOf(System.env.VERSION_CODE ?: 1)
def appVersionName = System.env.VERSION_NAME ?: "0.0"

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.20.1"
    }

    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
                kotlin {
                    option 'lite'
                }
            }
        }
    }
}

android {
    namespace 'io.dolby.rtsviewer'
    compileSdk 33

    defaultConfig {
        applicationId "io.dolby.ultra"
        minSdk 27
        targetSdk 33
        versionCode appVersionCode
        versionName appVersionName

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        String sharedTestDir = 'src/testCommon/java'
        test {
            java.srcDir(java.srcDirs + new File(projectDir, sharedTestDir))
        }
    }

    lint {
        baseline = file("lint-baseline.xml")
        abortOnError = true
        ignoreWarnings = false
        checkAllWarnings true
        warningsAsErrors false
        checkDependencies = true
    }

    signingConfigs {
        release {
            storeFile file('release.keystore')
            storePassword System.env.KEYSTORE_PASSWORD
            keyAlias System.env.KEY_ALIAS
            keyPassword System.env.KEY_PASSWORD
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            play {
                track.set("internal")
            }
        }
    }

    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion kotlin_compiler_version
    }

    flavorDimensions += "target"
    productFlavors {
        create("tv") {
            dimension = "target"
            setDefault(true)
        }
        create("mobile") {
            dimension = "target"
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation project(path: ':uikit')
    implementation project(path: ':rtscomponentkit')

    implementation libs.androidx.compose.ui
    implementation libs.androidx.compose.ui.preview

    implementation libs.androidx.core.ktx

    implementation libs.androidx.compose.material

    implementation libs.android.coroutines

    implementation "com.google.dagger:hilt-android:2.44"
    kapt "com.google.dagger:hilt-compiler:2.44"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0"

    tvImplementation 'androidx.leanback:leanback:1.0.0'

    tvImplementation libs.androidx.tv.foundation
    tvImplementation libs.androidx.tv.material

    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.1"
    implementation "androidx.navigation:navigation-compose:2.5.3"

    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.6.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-compose:2.6.1'
    implementation "androidx.activity:activity-compose:1.7.0"

    implementation "androidx.datastore:datastore:1.0.0"
    implementation 'com.google.protobuf:protobuf-kotlin-lite:3.21.2'

    implementation "androidx.datastore:datastore-core:1.0.0"
    implementation "androidx.datastore:datastore-preferences:1.0.0"

    testImplementation libs.junit
    testImplementation libs.robolectric
    testImplementation libs.androidx.compose.ui.test.junit
    debugImplementation libs.androidx.compose.ui.test.manifest
    testImplementation "androidx.navigation:navigation-testing:2.5.3"
    testImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_version"
    testImplementation("com.google.dagger:hilt-android-testing:2.44")
    kaptTest("com.google.dagger:hilt-android-compiler:2.44")
    testAnnotationProcessor("com.google.dagger:hilt-android-compiler:2.44")

    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation libs.androidx.compose.ui.test.junit
    androidTestImplementation("com.google.dagger:hilt-android-testing:2.44")
    kaptAndroidTest("com.google.dagger:hilt-android-compiler:2.44")
    androidTestImplementation "androidx.navigation:navigation-testing:2.5.3"
    androidTestImplementation "org.mockito.kotlin:mockito-kotlin:4.1.0"
    androidTestImplementation 'org.mockito:mockito-android:3.10.0'
}

// Allow references to generated code
kapt {
    correctErrorTypes true
}